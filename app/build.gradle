apply plugin: 'com.android.application'
apply from: "$rootDir/helpers.gradle"

//Api token can be defined as environment variable or in a file
//Exception message has more specific information where the api token should be placed
final APP_TOKEN_KEY = 'onfido_api_token'
final APP_TOKEN_FILE = 'secrets'
final APP_TOKEN_ENV_VAR_NAME = 'ONFIDO_API_TOKEN'
final ERROR_MESSAGE =   "You have not defined an app key for the Onfido api."

def apiKey = getValueFromFileOrEnv(APP_TOKEN_ENV_VAR_NAME, APP_TOKEN_FILE, APP_TOKEN_KEY, ERROR_MESSAGE)

def sdkVersion = '0.3.0PR'
def versionCodeNumber = 2

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.onfido.sampleapp"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode versionCodeNumber
        versionName "$versionCode-sdk:$sdkVersion"
        multiDexEnabled true
        injectInStringResources(it, APP_TOKEN_KEY, apiKey)
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    mavenLocal()
    maven { url  "https://dl.bintray.com/onfido/maven" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:multidex:'
    compile ("com.onfido.sdk.capture:onfido-capture-sdk:$sdkVersion")
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    testCompile 'junit:junit:4.12'
}