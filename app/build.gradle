apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.onfido.sampleapp"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    //Passing api token to binary
    //ref: https://gist.github.com/VDenis/46c222b16683447bab33
    //Api token can be defined as environment variable or in a file
    //Exception message has more specific information where the api token should be placed

    final APP_TOKEN_KEY = 'onfido_api_token'
    final APP_TOKEN_FILE = 'secrets'
    final APP_TOKEN_ENV_VAR_NAME = 'ONFIDO_API_TOKEN'
    final ERROR_MESSAGE =   "You have not defined an app key for the Onfido api."+
                            "\nYou can define "+APP_TOKEN_ENV_VAR_NAME+" as environment variable," +
                            " or put " + APP_TOKEN_KEY+"=KEY_HERE  in a file ./"+APP_TOKEN_FILE;

    def appTokenVal = System.getenv(APP_TOKEN_ENV_VAR_NAME)

    if (!appTokenVal) {
        try {
            Properties properties = new Properties()
            properties.load(project.rootProject.file(APP_TOKEN_FILE).newDataInputStream())
            appTokenVal = properties.getProperty(APP_TOKEN_KEY)
            if (appTokenVal == null){
                throw new InvalidUserDataException(ERROR_MESSAGE)
            }
        }
        catch (Exception e) {
            throw new InvalidUserDataException(e.message + "\n"+ERROR_MESSAGE);
        }
    }

    buildTypes {
        buildTypes.each {
            it.buildConfigField 'String', APP_TOKEN_KEY, '\"'+appTokenVal+'\"'
            it.resValue 'string', APP_TOKEN_KEY, appTokenVal
        }
    }
}



repositories {
    mavenLocal()
    maven {
        url  "https://dl.bintray.com/onfido/maven"
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile ("com.onfido.sdk.capture:onfido-capture-sdk:0.0.5")
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    testCompile 'junit:junit:4.12'
}